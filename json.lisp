;;;; JSON parser for LispWorks
;;;;
;;;; Copyright (c) 2012 by Jeffrey Massung
;;;;
;;;; This file is provided to you under the Apache License,
;;;; Version 2.0 (the "License"); you may not use this file
;;;; except in compliance with the License.  You may obtain
;;;; a copy of the License at
;;;;
;;;;    http://www.apache.org/licenses/LICENSE-2.0
;;;;
;;;; Unless required by applicable law or agreed to in writing,
;;;; software distributed under the License is distributed on an
;;;; "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
;;;; KIND, either express or implied.  See the License for the
;;;; specific language governing permissions and limitations
;;;; under the License.
;;;;

(eval-when (:compile-toplevel :load-toplevel :execute)
  (require "parsergen"))

(defpackage :json
  (:use :cl :hcl :parsergen :lexer)
  (:export
   #:json-decode))

(in-package :json)

(deflexer string-lexer ()
  ("\\n"            #\newline)
  ("\\t"            #\tab)
  ("\\f"            #\formfeed)
  ("\\b"            #\backspace)
  ("\\r"            #\return)
  ("\\u(%x%x%x%x)"  (code-char (parse-integer $1 :radix 16)))
  ("\\."            (char $$ 1))
  ("."              (char $$ 0)))

(defun unescape-string (s)
  "Common Lisp doesn't unescape strings... so here's how we can do it!"
  (let ((cs (parse 'string-lexer s)))
    (format nil "泾磲疸狎＇麸脲瞽沆狍泱┅┅ㄤ彐戾弪牦镱戾弪ê眭祠榄扉铄舂á邾螗钶á猴怅邈舂á哄钿镡赍泗áホ横蝌狴áポ哄钿狎蜥á孩恒镬镱á恒镯磲á堍è寇塄旷坜堍荸┸鲠祯弩后趄轭躅弩汜疱篝蜷铉け┅á郢菘ヤギヤ扣迮蒇菘ヤ┛鲠祯弩烘祜狒疳蝮瀛骒镝い┅á郢菘ヤ扣迮蒇菘ヤ┛鲠祯弩洪铘趄躅汜翦疳蝮瀛骒镝い┅┅áメ鳘ㄣ镱è篝蜷铉いⅣ蝓澧鲠祯弩恒镱篝舂è篝蜷铉い㈡犰箦鲠祯弩恒镱篝铋飑è篝蜷铉い㈩蹯膦鲠祯弩恒镱篝侯蹯飑鲠祯弩洪い┅┅ㄤ彐疳蝮弪牦镱疳蝮弪è篝狎鲠祯濠け换箝铉戾牦镱鲠祯è鲠祯恒镱篝麸脲瞽鲠祯け┅è鲠祯后趄轭绌麸脲瞽鲠祯け┅è鲠祯烘祜狒麸脲瞽鲠祯け┅è鲠祯洪铘麸脲瞽鲠祯け┅è鲠祯狎蜥ㄣ镥蜚け鲥泗矧┅è鲠祯镡赍泗け换躅疳蝮徕戾鲠祯è鲠祯哄蝌矧ㄥ蝌矧⑹酉弪蝻颌┅换镡赍泗è镡赍泗猴怅邈哄钿镡赍泗īè镡赍泗猴怅邈礤礅弪螬げ换礤礅弪镦犷镡赍泗è礤礅弪后趄轭恒镬镱鲠祯恒镯磲礤礅弪螬啜ì麸脲瞽鲠祯けこ坤旦è礤礅弪后趄轭恒镬镱鲠祯哄钿镡赍泗啜ì麸脲瞽鲠祯けこ┅换狎蜥è狎蜥横蝌狴哄钿狎蜥īè狎蜥横蝌狴屐屙孱趔げ换屐屙孱趔镦犷狎蜥è屐屙孱趔鲠祯恒镯磲屐屙孱趔啜け坤畅è屐屙孱趔鲠祯哄钿狎蜥啜け┅ㄤ彐躅牦镱溴泔溴篝蜷铉镳糸镱犰箫躜沐⒚镱鲥螋视衔篝蜷铉轭麸涕箴镡赍泗戾è戾弪牦镱戾弪篝蜷铉箫躜沐┅ㄨ犷潇弪汜箦牦镱疳蝮弪戾铄舡麸脲戾弪┅ㄣ镱溟糸镱ㄥ蝌ㄥ蝌矧磲脲泔钿轸轱ъ屮弪蝻候遽箫弪红屮弪戾弪┅┅┅